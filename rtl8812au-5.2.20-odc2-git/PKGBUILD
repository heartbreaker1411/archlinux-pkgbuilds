_p=rtl8812au
pkgname=rtl8812au-5.2.20-odc2-git
pkgver=5.2.20
pkgrel=1
pkgdesc="Realtek 8812AU USB WiFi module, based on Realtek sources v$pkgver"
arch=(aarch64)
url="https://github.com/mk-fg/rtl8812au"
license=(GPL-2)
depends=(dkms linux linux-headers)
makedepends=(git)
source=("$_p::git+$url")
sha256sums=(SKIP)

pkgver() {
	cd $_p
	rel=$( gawk \
		'/^#define\s+DRIVERVERSION\s+/ && match($3,/"v([0-9]\S*)"/,m) {print m[1]}' \
		include/rtw_version.h )
	[[ -n "$rel" ]] || exit 1
	printf "%s.%s.%s" "$rel" "$(git rev-list --count HEAD)" "$(git rev-parse --short HEAD)"
}

package() {
	cd $_p
	mkdir -p "$pkgdir/usr/src/$_p-$pkgver"
	cp -a * "$pkgdir/usr/src/$_p-$pkgver"
	install -Dm644 dkms.conf "$pkgdir/usr/src/$_p-$pkgver"
	sed -e "s/@PKGBASE@/$_p-dkms/" -e "s/@PKGVER@/$pkgver/" -i "$pkgdir/usr/src/$_p-$pkgver/dkms.conf"
}

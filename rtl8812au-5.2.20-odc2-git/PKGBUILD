pkgname=rtl8812au-5.2.20-odc2-git
_p=rtl8812au
_pv_suffix=''

pkgver=5.2.20
pkgrel=1
pkgdesc="Realtek 8812AU USB WiFi module (v$pkgver$_pv_suffix) for ODROID-C2"
arch=(aarch64)
url="https://github.com/mk-fg/rtl8812au"
license=(GPL-2)
depends=(dkms linux linux-headers)
makedepends=(git)
source=("$_p::git+$url")
sha256sums=(SKIP)

pkgver() {
	cd $_p
	rel=$( gawk \
		'/^#define\s+DRIVERVERSION\s+/ && match($3,/"v([0-9]\S*)"/,m) {print m[1]}' \
		include/rtw_version.h )
	[[ -n "$rel" ]] || exit 1
	printf "%s.%s.%s" "$rel" "$(git rev-list --count HEAD)" "$(git rev-parse --short HEAD)"
}

prepare() {
	cd $_p
	patch -p1 < Makefile.enable-odc2.patch
	sed -i '/DEBUG=/!s:^\(MAKE=".*\)":\1 DEBUG=2":' dkms.conf
}

package() {
	cd $_p
	local pv="${pkgver}${_pv_suffix}"
	mkdir -p "$pkgdir/usr/src/$_p-$pv"
	cp -a * "$pkgdir/usr/src/$_p-$pv"
	install -Dm644 dkms.conf "$pkgdir/usr/src/$_p-$pv"

	# dkms.conf package name/version
	sed \
		-e "s/@PKGBASE@/$_p-dkms/" \
		-e "s/@PKGVER@/$pv/" \
		-i "$pkgdir/usr/src/$_p-$pv/dkms.conf"

	# Replace MODULE_VERSION() to easily track which .ko is installed/loaded
	# Loaded version check example: systool -vm 8812au | grep version
	sed -e '/DRIVERVERSION/s|"\(.*\)"|"'"v$pv"'"|' \
		-i "$pkgdir/usr/src/$_p-$pv/include/rtw_version.h"
	# dkms will still not replace module in pacman hook if modpost yields same srcversion
	# So make sure that srcversion will depend on package version by altering .c file(s)
	echo "// Version Tag: v$pv" >>"$pkgdir/usr/src/$_p-$pv/os_dep/linux/os_intfs.c"
}
